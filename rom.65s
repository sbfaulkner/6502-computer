;*******************************************************************************
; Addresses and constants
PORTB = $6000
PORTA = $6001
DDRB = $6002
DDRA = $6003

E  = %10000000
RW = %01000000
RS = %00100000

;*******************************************************************************
; ROM start
        .ORG $8000

START:
        LDX #$FF                ; Initialize stack pointer
        TXS

        LDA #%11111111          ; Set all eight pins of ORB to output
        STA DDRB

        LDA #%11100000          ; Set top three pins of ORA to output
        STA DDRA

        LDA #%00111000          ; Set 8-bit mode; 2-line display; 5x8 font
        JSR LCD_INSTRUCTION
        LDA #%00001110          ; Display on; cursor on; blink off
        JSR LCD_INSTRUCTION
        LDA #%00000110          ; Advancing cursor; no shifting
        JSR LCD_INSTRUCTION
        LDA #%00000001          ; Clear display
        JSR LCD_INSTRUCTION

        LDA #"H"                ; Send H to DDRAM
        JSR PRINT_CHARACTER
        LDA #"e"                ; Send e to DDRAM
        JSR PRINT_CHARACTER
        LDA #"l"                ; Send l to DDRAM
        JSR PRINT_CHARACTER
        LDA #"l"                ; Send l to DDRAM
        JSR PRINT_CHARACTER
        LDA #"o"                ; Send o to DDRAM
        JSR PRINT_CHARACTER
        LDA #","                ; Send , to DDRAM
        JSR PRINT_CHARACTER
        LDA #" "                ; Send space to DDRAM
        JSR PRINT_CHARACTER
        LDA #"w"                ; Send w to DDRAM
        JSR PRINT_CHARACTER
        LDA #"o"                ; Send o to DDRAM
        JSR PRINT_CHARACTER
        LDA #"r"                ; Send r to DDRAM
        JSR PRINT_CHARACTER
        LDA #"l"                ; Send l to DDRAM
        JSR PRINT_CHARACTER
        LDA #"d"                ; Send d to DDRAM
        JSR PRINT_CHARACTER
        LDA #"!"                ; Send ! to DDRAM
        JSR PRINT_CHARACTER

LOOP:
        JMP LOOP                ; Stop here

LCD_WAIT:
        PHA
        LDA #%00000000          ; ORB is input
        STA DDRB
LCD_POLL:
        LDA #RW                 ; Set RW; Clear RS/E bits
        STA PORTA
        ORA #E                  ; Set E bit to send instruction
        STA PORTA
        LDA PORTB
        AND #%10000000          ; Check busy flag
        BNE LCD_POLL

        LDA #RW                 ; Clear E bit
        STA PORTA
        LDA #%11111111          ; ORB is output
        STA DDRB
        PLA
        RTS

LCD_INSTRUCTION:
        JSR LCD_WAIT
        STA PORTB
        LDA #0                  ; Clear RS/RW/E bits
        JSR LCD_SEND
        RTS

PRINT_CHARACTER:
        JSR LCD_WAIT
        STA PORTB
        LDA #RS                 ; Set RS; Clear RW/E bits
LCD_SEND:
        STA PORTA
        ORA #E                  ; Set E bit to send instruction
        STA PORTA
        EOR #E                  ; Clear E bit
        STA PORTA
        RTS

;*******************************************************************************
; Hardware vectors

        .ORG $FFFC
        .WORD START             ; RESET vector
        .WORD $0000
